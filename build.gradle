plugins {
    id "java"
    id "maven-publish"
    id "org.bookmc.buildtools" version "0.3.3+build.15"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

ext {
    def env = System.getenv()
    buildNumber = env.BUILD_NUMBER
    local = buildNumber == null
    branch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

group "org.bookmc"
version "0.1.0${branch != "main" ? "-$branch" : ""}+${buildNumber ? "build.$buildNumber" : "local"}"


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

repositories {
    mavenCentral()
    maven {
        name "Sponge Repo"
        url "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        url "https://fabricmc.net"
    }
}

dependencies {
    implementation("net.fabricmc:sponge-mixin:0.10.0+mixin.0.8.4.snapshot") {
        exclude module: "log4j-api"
        exclude module: "log4j-core"
        exclude module: "guava"
        exclude module: "gson"
    }

    implementation "com.google.code.gson:gson:2.8.8"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "book-loader"
                url = "https://github.com/BookMC/loader"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}